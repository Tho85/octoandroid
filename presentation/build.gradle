apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' // Dagger2
apply plugin: 'com.jakewharton.hugo' // Hugo

android {
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 16
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        applicationId "com.nairbspace.octoandroid"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        resValue "string", "google_app_id", MyGoogleAppId
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "my_ip_address", BlankIpAddress
            resValue "string", "my_api_key", BlankApiKey
        }
        debug {
            resValue "string", "my_ip_address", MyIpAddress
            resValue "string", "my_api_key", MyApiKey
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res/layouts/main',
                    'src/main/res/layouts/playback_controls',
                    'src/main/res/layouts/connection',
                    'src/main/res/layouts/status',
                    'src/main/res/layouts/files',
                    'src/main/res/layouts/add_printer',
                    'src/main/res/layouts',
                    'src/main/res'
            ]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile project(':data')
    compile project(':domain')

    // Support Library
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:percent:23.4.0'

    // QR Decoder
    compile 'com.google.android.gms:play-services-vision:9.0.0'

    // Butterknife
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    // RxJava WebSockets
    compile 'com.appunite:websockets-rxjava:4.0.1'

    // Dagger2
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'

    // Timber
    compile 'com.jakewharton.timber:timber:3.1.0'

    // EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'

    // AutoValue Parcelable
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.1'
}
